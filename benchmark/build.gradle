plugins {
    id 'com.android.test'
    id 'org.jetbrains.kotlin.android'
}

apply from: "$rootDir/dependencies.gradle"

android {
    namespace 'com.skydoves.disneycompose.benchmark'
    compileSdk versions.compileSdk

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    defaultConfig {
        minSdk versions.minSdk
        targetSdk versions.compileSdk
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        // This benchmark buildType is used for benchmarking, and should function like your
        // release build (for example, with minification on). It's signed with a debug key
        // for easy local/CI testing.
        benchmark {
            debuggable = true
            signingConfig = debug.signingConfig
            matchingFallbacks = ["release"]
        }
    }

    targetProjectPath = ":app"
    experimentalProperties["android.experimental.self-instrumenting"] = true
}

dependencies {
    implementation "androidx.profileinstaller:profileinstaller:$versions.baselineProfiles"
    implementation "androidx.benchmark:benchmark-macro-junit4:$versions.macroBenchmark"
    implementation "androidx.test.uiautomator:uiautomator:$versions.uiAutomator"
    implementation "com.android.support.test:runner:$versions.androidTestRunner"
}

androidComponents {
    beforeVariants(selector().all()) {
        enabled = buildType == "benchmark"
    }
}

apply from: "$rootDir/spotless.gradle"